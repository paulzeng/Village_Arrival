apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: '../dependency.gradle'

repositories {
    mavenCentral()
    flatDir { dir 'libs' }
}

android {
    compileSdkVersion versions.compileSdkVersion
    defaultConfig {
        applicationId "com.ruanmeng.village_arrival"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 100
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.all { output ->
                    def file = output.outputFile
                    if (file != null && file.name.endsWith('.apk')) {
                        // def version = "v${versionName}(${versionCode})"
                        // def formatDate = new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("UTC"))
                        // def fileName = file.name.replace(".apk", "-${version}-${formatDate}.apk")
                        // def fileName = file.name.replace(".apk", "-v${versionName}.apk")
                        def fileName = "${applicationId}-v${versionName}.apk"
                        outputFileName = fileName
                    }
                }
            }
        }

        //支持 armeabi-v7a 和 x86 架构 CPU
        //处理.so文件时的重要法则:要么全部支持，要么都不支持
        ndk { abiFilters "armeabi-v7a", "x86" }

        //高德地图的apikey，友盟分享QQ的appid，极光推送的pkgname、key、channel
        manifestPlaceholders = [
                amap_apikey: "f1b683336c90bbc2d65a2921fe18f2a9",
                qqappid: "100424468",
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY: "d53198e7070dab6428b23f27",
                JPUSH_CHANNEL: "developer-default"
        ]
    }

    buildTypes {
        debug {
            //显示Log打印输出
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "API_HOST", "\"http://122.114.212.120:8085\""
            minifyEnabled false   //混淆编译
            zipAlignEnabled false //Zip代码压缩优化
        }
        release {
            //不显示Log打印输出
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "API_HOST", "\"http://122.114.212.120:8085\""
            minifyEnabled false  //混淆编译
            zipAlignEnabled true //Zip代码压缩优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../Village_Arrival.jks')
            storePassword "123456"
            keyAlias "Village_Arrival"
            keyPassword "123456"
        }
    }

    //移除lint检查的error（程序在buid的时候，会执行lint检查）
    lintOptions {
        abortOnError false  // true--错误发生后停止gradle构建
        ignoreWarnings true // true--只报告error
        showAll true        // true--显示错误的所有发生位置，不截取
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main { jniLibs.srcDirs = ['libs'] }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:appcompat-v7:' + versions.supportLibraryVersion
    implementation 'com.android.support:recyclerview-v7:' + versions.supportLibraryVersion
    implementation 'com.android.support:cardview-v7:' + versions.supportLibraryVersion
    implementation 'com.android.support:design:' + versions.supportLibraryVersion
    implementation 'com.android.support:support-v4:' + versions.supportLibraryVersion
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:' + versions.kotlinPlugin
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:multidex:' + versions.multidexVersion
    testImplementation 'junit:junit:4.12'

    implementation project(':AMap')
    implementation project(':okgo')
    implementation(name: 'BouncingJelly-1.0.4', ext: 'aar')
    implementation(name: 'FlycoDialog-1.3.2', ext: 'aar')
    implementation(name: 'GlideImageView-1.3.0', ext: 'aar')
    implementation(name: 'MNProgressHUD-1.0.1', ext: 'aar')
    implementation(name: 'RatingBar-1.3.2', ext: 'aar')
    implementation(name: 'RollViewPager', ext: 'aar')
    implementation(name: 'SuperText-1.1.1', ext: 'aar')
    implementation(name: 'UmengSocial-6.9.1', ext: 'aar')

    //图片浏览
    implementation(name: 'MNImageBrowser-1.0.3', ext: 'aar')
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    //图片加载
    implementation 'com.github.bumptech.glide:glide:' + versions.glideVersion
    kapt 'com.github.bumptech.glide:compiler:' + versions.glideVersion
    implementation "com.github.bumptech.glide:okhttp3-integration:" + versions.glideVersion

    //图片选择
    implementation('com.github.LuckSiege.PictureSelector:picture_library:v2.2.3') {
        exclude group: 'com.github.bumptech.glide', module: 'glide'
    }

    //版本更新
    implementation('com.qianwen:update-app-kotlin:1.2.1') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre7'
    }

    //友盟分享
    implementation 'com.umeng.sdk:share-core:' + versions.umengSocialVersion
    implementation 'com.umeng.sdk:shareboard-widget:' + versions.umengSocialVersion

    //极光推送
    implementation 'cn.jiguang.sdk:jpush:' + versions.jpushSdkVersion
    implementation 'cn.jiguang.sdk:jcore:' + versions.jpushCoreVersion

    //在线支付
    implementation 'com.cuieney:rxpay-api:2.1.7'
    kapt 'com.cuieney:rxpay-compiler:2.1.1'

    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'
    implementation 'com.github.lguipeng:BubbleView:1.0.1'
    implementation 'com.github.santalu:mask-edittext:1.0.1'
    implementation 'com.weigan:loopView:0.1.2'
    implementation 'com.yanzhenjie:permission:1.1.2'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'net.idik:slimadapter:2.1.2'
    implementation 'org.greenrobot:eventbus:3.0.0'
}
